<?wh
LOADLIB "wh::files.whlib";
LOADLIB "module::system/configure.whlib";
LOADLIB "module::system/internal/moduleimexport.whlib";
LOADLIB "module::tollium/screenbase.whlib";
LOADLIB "module::publisher/siteprofiles.whlib";
LOADLIB "relative::../siteprofiles.whlib";

BOOLEAN debug;
BOOLEAN initdone;

PUBLIC OBJECTTYPE Main EXTEND TolliumScreenBase
<
  BOOLEAN initdone;

  MACRO Init(RECORD data)
  {
    IF ("debug" IN data.params)
    {
      debug := TRUE;
    }

    STRING regsettings := ReadRegistryKey("codegenerator.siteprofiles.settings");
    RECORD prefillsettings := regsettings = "" ? DEFAULT RECORD : DecodeHSON(regsettings);

    IF (RecordExists(prefillsettings))
    {
      FOREVERY (STRING setting FROM ["type","baseurl","itemtitle","lib","members","use_rtd","use_tids","form_tag","is_prebuilt_page","prebuilt_page_is_static"])
      {
        IF (CellExists(prefillsettings, setting))
          GetMember(this, setting)->value := GetCell(prefillsettings, setting);
      }

      this->DoGenerateCode();

      this->initdone := TRUE;
    }

    this->members->onrowedit := PTR this->DoOpenMembersEditDialog;

    //this->maintabs->selectedtab := this->objectscaffolding;

    IF ("debug" IN data.params)
    {
      this->members->value := [[ title := "A text", name := "", type := "string", required := FALSE, members := DEFAULT RECORD ARRAY ]
                              ,[ title := "A textarea", name := "", type := "textarea", required := TRUE, members := DEFAULT RECORD ARRAY ]
                              ,[ title := "An image", name := "", type := "image", required := TRUE, members := DEFAULT RECORD ARRAY ]
                              ,[ title := "An intextlink", name := "", type := "intextlink", required := TRUE, members := DEFAULT RECORD ARRAY ]
                              ,[ title := "A date", name := "", type := "datetime", required := TRUE, members := DEFAULT RECORD ARRAY
                               , type_dt_datetimetype := "date"
                               , type_dt_precision := "minutes"
                               , type_dt_storeutc := FALSE
                               ]
                              ,[ title := "A boolean", name := "", type := "boolean", required := TRUE, members := DEFAULT RECORD ARRAY ]
                              ,[ title := "A richarea", name := "", type := "richarea", required := FALSE, members := DEFAULT RECORD ARRAY ]
                              ,[ title := "A richdocument", name := "", type := "richdocument", required := FALSE, members := DEFAULT RECORD ARRAY ]
                              ,[ title := "A whfsref", name := "", type := "whfsref", required := TRUE, members := DEFAULT RECORD ARRAY ]
                              ,[ title := "An array", name := "", type := "array", required := FALSE
                               , members := [[ title := "A text", name := "", type := "string", required := FALSE, members := DEFAULT RECORD ARRAY ]
                                            ,[ title := "An image", name := "", type := "image", required := TRUE, members := DEFAULT RECORD ARRAY ]
                                            ,[ title := "A boolean", name := "", type := "boolean", required := TRUE, members := DEFAULT RECORD ARRAY ]
                                            ,[ title := "A richdocument", name := "", type := "richdocument", required := FALSE, members := DEFAULT RECORD ARRAY ]
                                            ,[ title := "A whfsref", name := "", type := "whfsref", required := TRUE, members := DEFAULT RECORD ARRAY ]
                                            ]
                               ]
                              ];

      this->itemtitle->value := "Project";
      this->lib->value := "project";
    }
    ELSE IF ("create" IN data.params)
    {
      this->DoCreateTemplate();
    }

    this->SetMainTab();

    initdone := TRUE;
  }

  RECORD FUNCTION DoOpenMembersEditDialog(RECORD row)
  {
    OBJECT dialog := this->LoadScreen(".editmember"
                                    , [ row := row
                                      ]);

    IF (dialog->RunModal() = "ok")
      RETURN dialog->result;

    RETURN DEFAULT RECORD;
  }

  MACRO OnAnyChange()
  {
    //this->index_is_rtd->enabled := NOT this->use_rtd->value AND NOT this->page_only->value;
    //this->use_rtd->enabled := this->type->value != "folderwithindex";
    this->prebuilt_page_is_static->enabled := ^type->value IN ["folderwithindex","page_only"] AND this->is_prebuilt_page->value;

    ^use_rtd->enabled := ^type->value IN ["folderwithindex","page_only"];
    ^is_prebuilt_page->enabled := ^type->value IN ["folderwithindex","page_only"];

    IF (NOT debug AND initdone)
    {
      this->DoSaveSettings();
    }

    IF (this->initdone)
      this->DoGenerateCode();

    this->SetMainTab();
  }

  MACRO SetMainTab()
  {
    this->maintabs->selectedtab := this->type->value = "embeddedobject" ? this->main_embobj : this->main_objectscaffolding;
  }

  MACRO DoGenerateCode()
  {
    RECORD ARRAY members := SELECT *
                                 , name := (name != "" ? name : GetSiteProfileXMLName(title))
                                 , comments := ""
                                 , options := DEFAULT RECORD ARRAY
                                 , settings := [ types := DEFAULT RECORD ARRAY ]
                                 , members := (NOT CellExists(mainmembers,"members") ? DEFAULT RECORD ARRAY : mainmembers.members)
                              FROM this->members->value AS mainmembers;

    FOREVERY (RECORD siteprofmember FROM members)
    {
      IF (siteprofmember.type = "array")
      {
        members[#siteprofmember].members := SELECT *
                                                 , name := (name != "" ? name : GetSiteProfileXMLName(title))
                                                 , comments := ""
                                                 , options := DEFAULT RECORD ARRAY
                                              FROM siteprofmember.members;
      }
    }

    RECORD data := [ name := GetSiteProfileXMLName(this->itemtitle->value)
                   , title := this->itemtitle->value//Substitute(this->itemtitle->value, " ", "")
                   , originaltitle := this->itemtitle->value
                   //, filetypes := [[ title := this->CapitalizeFirstChar(this->itemtitle->value), isrtd := this->use_rtd->value ]]
                   , foldertype := [ title := this->CapitalizeFirstChar(this->itemtitle->value) ]
                   , members := members
                   , screen_only := FALSE
                   , page_only := this->type->value = "page_only"
                   , codetype := ^type->value
                   ];

    FOREVERY (STRING setting FROM ["lib","use_rtd","use_tids","is_prebuilt_page","prebuilt_page_is_static"])
    {
      data := CellInsert(data, setting, GetMember(this, setting)->value);
    }

    IF (this->type->value = "folderwithindex")
    {
      //UPDATE data.filetypes SET isrtd := TRUE;

      //INSERT CELL filetype_is_rtd := TRUE INTO data;
    }

    //IF (this->index_is_rtd->value = TRUE)
    //  INSERT CELL index_is_rtd := TRUE INTO data;

    this->GenerateCode(data);
  }

  STRING FUNCTION CapitalizeFirstChar(STRING text)
  {
    RETURN ToUpperCase(Left(text,1)) || Substring(text, 1);
  }

  RECORD FUNCTION ConvertToExpectedData(RECORD data)
  {
    FOREVERY (RECORD siteprofmember FROM data.members)
    {
      SWITCH (siteprofmember.type)
      {
        CASE "array"
        {
          FOREVERY (RECORD mem FROM siteprofmember.members)
            siteprofmember.members[#mem] := this->ApplyConvertToExpectedData(mem);
        }
        DEFAULT
        {
          siteprofmember := this->ApplyConvertToExpectedData(siteprofmember);
        }
      }

      data.members[#siteprofmember] := siteprofmember;
    }

    RETURN data;
  }

  RECORD FUNCTION ApplyConvertToExpectedData(RECORD siteprofmember)
  {
    IF (NOT CellExists(siteprofmember, "settings"))
      siteprofmember := CellInsert(siteprofmember, "settings", DEFAULT RECORD);

    SWITCH (siteprofmember.type)
    {
      CASE "datetime"
      {
        siteprofmember.settings := CellInsert(siteprofmember.settings, "datetype", siteprofmember.type_dt_datetimetype);
        siteprofmember.settings := CellInsert(siteprofmember.settings, "precision", siteprofmember.type_dt_precision);
        siteprofmember.settings := CellInsert(siteprofmember.settings, "storeutc", siteprofmember.type_dt_storeutc);
      }
    }

    RETURN siteprofmember;
  }

  MACRO GenerateCode(RECORD data)
  {
    data := this->ConvertToExpectedData(data);

    this->siteprlcode->value := GenerateSiteProfile(this->baseurl->value, data);
    this->libcode->value := GenerateSiteProfileLibrary(this->baseurl->value, data);
    this->wittycode->value := GenerateSiteProfileWitty(data);

    RECORD embdata := [ ...data, name := this->lib->value ];
    this->emb_siteprlcode->value := GenerateSiteProfileEmbObj(this->baseurl->value, embdata);
    this->emb_libcode->value := GenerateSiteProfileLibraryEmbObj(this->baseurl->value, embdata);
    this->emb_wittycode->value := GenerateSiteProfileWittyEmbObj(embdata);

    RECORD formdata := [ ...data, form_tag := `${this->form_tag->value}form` ];
    this->form_formdefcode->value := GenerateFormFormDefXML(formdata);
    this->form_libcode->value := GenerateFormLibrary(formdata);
    this->form_wittycode->value := GenerateFormWitty(formdata);
    this->form_jscode->value := GenerateJavaScriptForm(formdata);
  }

  MACRO DoSaveSettings() // save settings as HSON
  {
    STRING ARRAY settings_sa :=
        ["baseurl","itemtitle","lib","members","type","use_rtd","use_tids","form_tag","is_prebuilt_page","prebuilt_page_is_static"];

    RECORD settings;
    FOREVERY (STRING setting FROM settings_sa)
      settings := CellInsert(settings, setting, GetMember(this, setting)->value);

    OBJECT work := this->BeginWork();
    WriteRegistryKey("codegenerator.siteprofiles.settings", EncodeHSON(settings));
    work->Finish();
  }

  MACRO DoCreateTemplate()
  {
    OBJECT scr := this->LoadScreen(".createtemplate", [ libcode := this->libcode->value
                                                      , wittycode := this->wittycode->value
                                                      , siteprlcode := this->siteprlcode->value
                                                      , tag := this->lib->value
                                                      ]);

    IF (scr->RunModal() = "ok")
    {
      scr->LoadScreen(".templatecreated", [ add_js := scr->add_js
                                          , add_siteprl := scr->add_siteprl
                                          ])->RunModal();
    }
  }

  MACRO docreateembeddedobjectfolder()
  {
    OBJECT scr := this->LoadScreen(".createembeddedobjectfolder", [ libcode := this->emb_libcode->value
                                                                  , wittycode := this->emb_wittycode->value
                                                                  , siteprlcode := this->emb_siteprlcode->value
                                                                  , tag := this->lib->value
                                                                  ]);

    IF (scr->RunModal() = "ok")
    {
      scr->LoadScreen(".templatecreatedembobj", [ add_js := scr->add_js
                                                , add_siteprl := scr->add_siteprl
                                                , add_rtdtype := `${FixBaseURL(this->baseurl->value)}widgets/${this->lib->value}`
                                                ])->RunModal();
    }
  }

  MACRO DoCreateFormFolder()
  {
    OBJECT scr := this->LoadScreen(".createformfolder", [ libcode := this->form_libcode->value
                                                        , wittycode := this->form_wittycode->value
                                                        , formdefcode := this->form_formdefcode->value
                                                        , jscode := this->form_jscode->value
                                                        , tag := this->form_tag->value
                                                        ]);

    IF (scr->RunModal() = "ok")
    {
      scr->LoadScreen(".templatecreatedform", [ add_whlib := scr->add_whlib
                                              , add_siteprl := scr->add_siteprl
                                              , add_witty := scr->add_witty
                                              , add_js := scr->add_js
                                              ])->RunModal();
    }
  }
>;

PUBLIC OBJECTTYPE EditMember EXTEND TolliumScreenBase
<
  PUBLIC RECORD result;

  MACRO Init(RECORD data)
  {
    this->row->value := data.row;
    this->members->onrowedit := PTR this->DoOpenArrayMembersEditDialog;
  }

  RECORD FUNCTION DoOpenArrayMembersEditDialog(RECORD row)
  {
    OBJECT dialog := this->LoadScreen(".editarraymember"
                                    , [ row := row
                                      ]);

    IF (dialog->RunModal() = "ok")
      RETURN dialog->result;

    RETURN DEFAULT RECORD;
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();
    this->result := this->row->value;
    RETURN work->Finish();
  }
>;

PUBLIC OBJECTTYPE EditArrayMember EXTEND TolliumScreenBase
<
  PUBLIC RECORD result;

  MACRO Init(RECORD data)
  {
    this->row->value := data.row;
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();
    this->result := this->row->value;
    RETURN work->Finish();
  }
>;

PUBLIC OBJECTTYPE CreateTemplate EXTEND TolliumScreenBase
<
  RECORD ARRAY sitedesigns;
  STRING libcode;
  STRING tag;
  STRING wittycode;
  STRING siteprlcode;

  PUBLIC STRING add_js;
  PUBLIC STRING add_siteprl;

  MACRO Init(RECORD data)
  {
    this->libcode := data.libcode;
    this->wittycode := data.wittycode;
    this->siteprlcode := data.siteprlcode;
    this->tag := data.tag;

    this->module->options := __GetWebDesigns();

    this->UpdateExplanation();
  }

  PUBLIC MACRO UpdateExplanation()
  {
    RECORD templatesettings := GetTemplateSettings(this->sitedesign->value, this->module->value);
    this->explanation->value := GetTid("codegenerator:createtemplate.explanation", this->tag, templatesettings.web_site_design);
  }

  MACRO OnModuleChange()
  {
    RECORD selected_module := SELECT * FROM this->module->options WHERE tolliumselected;

    this->sitedesign->options := selected_module.webdesigns;

    this->sitedesign->readonly := Length(this->sitedesign->options) = 1;

    this->UpdateExplanation();
  }

  BOOLEAN FUNCTION Submit()
  {
    RECORD settings := GetTemplateSettings(this->sitedesign->value, this->module->value);

    // 1) create a folder 'TAG' in src/pages/
    STRING newfolderpath := settings.sitedesign_path || "pages/" || this->tag || "/";
    CreateDiskDirectory(newfolderpath, TRUE);

    // 2) create WHLIB, Witty, SCSS and JS (.es) files in the new folder
    // 2a) WHLIB
    IF ("whlib" IN this->addtypes->value)
    {
      STRING whlib := this->libcode;
      INTEGER addpagenamepos := SearchSubString(ToUpperCase(whlib), "THIS->ISINDEX := WEBDESIGN->TARGETFOLDER->INDEXDOC = WEBDESIGN->TARGETOBJECT->ID");
      IF (addpagenamepos > -1)
      {
        whlib := Left(whlib, addpagenamepos)
              || 'INSERT "page-' || this->tag || '" INTO webdesign->htmlclasses AT END;\n\n    '
              || SubString(whlib, addpagenamepos);
      }

      CreateDiskFileFromBlob(newfolderpath || this->tag || ".whlib", FALSE, FALSE, StringToBlob(whlib));
    }

    // 2b) Witty
    IF ("witty" IN this->addtypes->value)
    {
      CreateDiskFileFromBlob(newfolderpath || this->tag || ".witty", FALSE, FALSE, StringToBlob(this->wittycode));
    }

    // 2c) SCSS
    IF ("css" IN this->addtypes->value)
    {
      STRING scss := "// @import '../../shared/css/vars';\n";
      CreateDiskFileFromBlob(newfolderpath || this->tag || ".scss", FALSE, FALSE, StringToBlob(scss));
    }

    // 2d) JS
    IF ("javascript" IN this->addtypes->value)
    {
      STRING js := "import './" || this->tag || ".scss';\n"
                || "\n"
                || "import * as dompack from 'dompack';\n"
                || "\n"
                || "dompack.onDomReady(() => {\n"
                || "  if (!document.documentElement.classList.contains('page-" || this->tag || "'))\n"
                || "    return;\n"
                || "\n"
                || "  console.info('page: " || this->tag || "');\n"
                || "});\n";

      CreateDiskFileFromBlob(newfolderpath || this->tag || ".es", FALSE, FALSE, StringToBlob(js));
    }

    // 2e) site profile
    IF ("siteprl" IN this->addtypes->value)
    {
      STRING siteprl := '<?xml version="1.0" encoding="UTF-8" ?>\n'
                     || '<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile"\n'
                     || '             >\n'
                     || '\n'
                     || this->siteprlcode || '\n'
                     || '\n'
                     || '</siteprofile>\n';

      CreateDiskFileFromBlob(newfolderpath || this->tag || ".siteprl.xml", FALSE, FALSE, StringToBlob(siteprl));
    }

    this->add_js := "import './pages/" || this->tag || "/" || this->tag || "';";
    this->add_siteprl := '<applysiteprofile path="pages/' || this->tag || '/' || this->tag || '.siteprl.xml" />';

    RETURN TRUE;
  }
>;

PUBLIC OBJECTTYPE TemplateCreated EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    this->add_js->value := data.add_js;
    this->add_siteprl->value := data.add_siteprl;
  }
>;

PUBLIC OBJECTTYPE CreateEmbeddedObjectFolder EXTEND TolliumScreenBase
<
  RECORD ARRAY sitedesigns;
  STRING libcode;
  STRING tag;
  STRING wittycode;
  STRING siteprlcode;

  PUBLIC STRING add_js;
  PUBLIC STRING add_siteprl;

  MACRO Init(RECORD data)
  {
    this->libcode := data.libcode;
    this->wittycode := data.wittycode;
    this->siteprlcode := data.siteprlcode;
    this->tag := data.tag;

    this->module->options := __GetWebDesigns();

    this->UpdateExplanation();
  }

  PUBLIC MACRO UpdateExplanation()
  {
    RECORD templatesettings := this->GetTemplateSettings();
    this->explanation->value := GetTid("codegenerator:createembeddedobjectfolder.explanation", this->tag, templatesettings.web_site_design);
  }

  MACRO OnModuleChange()
  {
    RECORD selected_module := SELECT * FROM this->module->options WHERE tolliumselected;

    this->sitedesign->options := selected_module.webdesigns;

    this->sitedesign->readonly := Length(this->sitedesign->options) = 1;

    this->UpdateExplanation();
  }

  RECORD FUNCTION GetTemplateSettings()
  {
    RETURN GetTemplateSettings(this->sitedesign->value, this->module->value);
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();

    RECORD settings := this->GetTemplateSettings();

    // check for folder 'widgets'
    RECORD ARRAY widgetscontents := ReadDiskDirectory(settings.sitedesign_path || "widgets/", "*");
    IF (Length(widgetscontents) = 0) // FIXME: better way to check if a folder exists?
    {
      TRY
      {
        CreateDiskDirectory(settings.sitedesign_path || "widgets", TRUE);
      }
      CATCH (OBJECT e)
      {
        work->AddError(e->what);
        RETURN work->Finish();
      }
      //work->AddError(`Directory '${settings.sitedesign_path}widgets/' doesn't seem to exist (please add a file if it's empty)`);
    }

    // 1) create a folder 'TAG' in src/widgets/
    STRING newfolderpath := settings.sitedesign_path || "widgets/" || this->tag || "/";
    RECORD ARRAY newfoldercontents := ReadDiskDirectory(newfolderpath, "*");
    //IF (Length(newfoldercontents) > 0) // FIXME: better way to check if a folder exists?
    //{
    //  work->AddError(`Directory '${newfolderpath} already exists and we will not overwrite it`);
    //  RETURN work->Finish();
    //}

    CreateDiskDirectory(newfolderpath, TRUE);

    // 2) create WHLIB, Witty, SCSS and JS (.es) files in the new folder
    // 2a) WHLIB
    IF ("whlib" IN this->addtypes->value)
      CreateDiskFileFromBlob(newfolderpath || this->tag || ".whlib", TRUE, FALSE, StringToBlob(this->libcode));

    // 2b) Witty
    IF ("witty" IN this->addtypes->value)
    {
      CreateDiskFileFromBlob(newfolderpath || this->tag || ".witty", TRUE, FALSE, StringToBlob(this->wittycode));
    }

    IF ("css" IN this->addtypes->value)
    {
      // 2c) SCSS
      STRING scss :=
`//@import '../../shared/css/vars';

.w-${this->tag} {

}\n`;

      CreateDiskFileFromBlob(newfolderpath || this->tag || ".scss", TRUE, FALSE, StringToBlob(scss));
    }

    IF ("javascript" IN this->addtypes->value)
    {
      // 2d) JS
      STRING js := "import './" || this->tag || ".scss';\n"
                || "\n"
                || "import * as dompack from 'dompack';\n"
                || "\n"
                || "dompack.onDomReady(() => {\n"
                || "});\n";

      CreateDiskFileFromBlob(newfolderpath || this->tag || ".es", TRUE, FALSE, StringToBlob(js));
    }

    // 2e) site profile
    IF ("siteprl" IN this->addtypes->value)
    {
      CreateDiskFileFromBlob(newfolderpath || this->tag || ".siteprl.xml", FALSE, FALSE, StringToBlob(this->siteprlcode));
    }

    this->add_js := "import './" || this->tag || "/" || this->tag || "';";
    this->add_siteprl := '  <applysiteprofile path="' || this->tag || '/' || this->tag || '.siteprl.xml" />';

    RETURN work->Finish();
  }
>;

PUBLIC OBJECTTYPE TemplateCreatedEmbObj EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    this->add_js->value := data.add_js;
    this->add_siteprl->value := data.add_siteprl;
    this->add_rtdtype->value :=
`<widgets>
  <allowtype type="${data.add_rtdtype}" />
</widgets>`;
  }
>;

PUBLIC OBJECTTYPE CreateFormFolder EXTEND TolliumScreenBase
<
  RECORD ARRAY sitedesigns;
  STRING libcode;
  STRING wittycode;
  STRING formdefcode;
  STRING jscode;
  STRING tag;

  PUBLIC STRING add_whlib;
  PUBLIC STRING add_siteprl;
  PUBLIC STRING add_witty;
  PUBLIC STRING add_js;

  MACRO Init(RECORD data)
  {
    this->libcode := data.libcode;
    this->wittycode := data.wittycode;
    this->formdefcode := data.formdefcode;
    this->jscode := data.jscode;
    this->tag := data.tag;

    this->module->options := __GetWebDesigns();

    this->UpdateExplanation();
  }

  PUBLIC MACRO UpdateExplanation()
  {
    RECORD templatesettings := this->GetTemplateSettings();
    this->explanation->value := GetTid("codegenerator:createformfolder.explanation", ToLowerCase(this->tag || "form"), templatesettings.web_site_design);
  }

  MACRO OnModuleChange()
  {
    OnModuleChange(this);
  }

  RECORD FUNCTION GetTemplateSettings()
  {
    RETURN GetTemplateSettings(this->sitedesign->value, this->module->value);
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();

    RECORD settings := this->GetTemplateSettings();

    STRING tag := ToLowerCase(this->tag);

    // 1) create a folder 'TAG' in src/
    STRING newfolderpath := settings.sitedesign_path || tag || "form/";
    RECORD ARRAY newfoldercontents := ReadDiskDirectory(newfolderpath, "*");
    IF (Length(newfoldercontents) > 0) // FIXME: better way to check if a folder exists?
    {
      work->AddError(`Directory '${newfolderpath} already exists and we will not overwrite it`);
      RETURN work->Finish();
    }

    CreateDiskDirectory(newfolderpath, TRUE);

    STRING libcode := Substitute(this->libcode, "<module>", this->module->value);
    STRING wittycode := Substitute(this->wittycode, "<module>", this->module->value);
    STRING formdefcode := Substitute(this->formdefcode, "<module>", this->module->value);
    STRING jscode := Substitute(this->jscode, "<module>", this->module->value);

    // 2) create WHLIB, Witty, formdef (XML) and JS (es) files in the new folder
    // 2a) WHLIB
    CreateDiskFileFromBlob(newfolderpath || tag || "form.whlib", TRUE, FALSE, StringToBlob(libcode));
    CreateDiskFileFromBlob(newfolderpath || tag || "form.witty", TRUE, FALSE, StringToBlob(wittycode));
    CreateDiskFileFromBlob(newfolderpath || tag || "form.formdef.xml", TRUE, FALSE, StringToBlob(formdefcode));
    CreateDiskFileFromBlob(newfolderpath || tag || "form.es", TRUE, FALSE, StringToBlob(jscode));

    this->add_whlib := `${tag}form := webdesign->GetWittyDataForForm("${tag}form")`;
    this->add_siteprl := `<formdefinitions path="${tag}form/${tag}form.formdef.xml" />`;
    this->add_witty := `[embed ../../${tag}form/${tag}form.witty:${tag}form]`;
    this->add_js := `import './${tag}form/${tag}form';`;

    RETURN work->Finish();
  }
>;

PUBLIC OBJECTTYPE TemplateCreatedForm EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    this->add_whlib->value := data.add_whlib;
    this->add_siteprl->value := data.add_siteprl;
    this->add_witty->value := data.add_witty;
    this->add_js->value := data.add_js;
  }
>;

RECORD ARRAY FUNCTION __GetWebDesigns()
{
  // collect webdesigns
  // get module name from cell designroot: 'mod::tollium/webdesigns/webinterface/'
  RECORD ARRAY sitedesigns := SELECT *
                                   , module := Tokenize(Tokenize(designroot, "::")[1], "/")[0]
                                FROM GetAvailableWebDesigns(FALSE);

  RECORD ARRAY modules := SELECT title := name
                               , rowkey := name
                               , webdesigns := DEFAULT RECORD ARRAY
                            FROM GetInstalledModulesOverview(FALSE)
                           WHERE NOT isdeleted
                                 AND NOT isbuiltin
                        ORDER BY ToUpperCase(name);

  FOREVERY (RECORD module FROM modules)
  {
    modules[#module].webdesigns := SELECT title := Substitute(designroot, "mod::" || COLUMN module || "/", "")
                                        , rowkey := designroot
                                     FROM sitedesigns
                                    WHERE ToUpperCase(COLUMN module) = ToUpperCase(module.rowkey)
                                 ORDER BY ToUpperCase(title);
  }

  DELETE FROM modules WHERE length(webdesigns) = 0; // we're not interested in these

  RETURN modules;
}

RECORD FUNCTION GetTemplateSettings(STRING sitedesign, STRING module)
{
  // strip the "webdesigns/SITEDESIGN" part from "mod::MODULE/webdesigns/SITEDESIGN/"
  STRING web_site_design := Substitute(sitedesign, "mod::" || module || "/", "");

  // get the sitedesign path (the folder containing the siteprl, package.json, etc)
  RETURN [ web_site_design := web_site_design
         , sitedesign_path := GetModuleInstallationRoot(module) || web_site_design
         ];
}

MACRO OnModuleChange(OBJECT screenbase)
{
  RECORD selected_module := SELECT * FROM screenbase->module->options WHERE tolliumselected;

  screenbase->sitedesign->options := selected_module.webdesigns;

  screenbase->sitedesign->readonly := Length(screenbase->sitedesign->options) = 1;

  screenbase->UpdateExplanation();
}

STRING FUNCTION FixBaseURL(STRING baseurl)
{
  IF (Right(baseurl, 1) != "/")
    baseurl := baseurl || "/";

  RETURN baseurl;
}
