<?wh
LOADLIB "wh::files.whlib";

STRING defaultboxname := "Instellingen";
//STRING defaultboxname := lang = "nl" ? "Instellingen" : "Settings";

RECORD screendata;
STRING screenbaseurl;

PUBLIC STRING FUNCTION GenerateSiteprofile(STRING baseurl, RECORD siteprofilesection)
{
  screendata := siteprofilesection;

  IF (Right(baseurl, 1) != "/")
    baseurl := baseurl || "/";

  screenbaseurl := baseurl;

  STRING o;

  IF (siteprofilesection.add_custom_rtdtype)
  {
    o := o ||
`  <rtdtype namespace="${baseurl}rtd/${siteprofilesection.name}">
    <!-- <css path="../../shared/rtd.css" /> -->
    <blockstyles defaultstyle="NORMAL">
      <textstyle tag="NORMAL" textstyles="b i a-href sup sub strike" />
    </blockstyles>
  </rtdtype>

`;
  }

  FOREVERY (RECORD foldertype FROM siteprofilesection.foldertypes)
  {
    o := o || GetFolderType(foldertype, screenbaseurl) || "\n\n";
  }

  FOREVERY (RECORD filetype FROM siteprofilesection.filetypes)
  {
    o := o || GetFileType(filetype, screenbaseurl)
           || "\n\n";
  }

  // add <contenttye> with <member>s and <propertyeditor>s
  IF (Length(siteprofilesection.members) = 0) // no members, just a <contenttype> then
  {
    o := o || '  <contenttype namespace="' || screenbaseurl || 'filetype/' || GetSiteProfileXMLName(siteprofilesection.name) || '" />\n\n';
  }
  ELSE
  {
    o := o || '  <contenttype namespace="' || screenbaseurl || 'filetype/' || GetSiteProfileXMLName(siteprofilesection.name) || '">\n';

    RECORD ARRAY members;
    FOREVERY (RECORD mem FROM siteprofilesection.members)
    {
      IF (NOT RecordExists(SELECT FROM members WHERE ToUpperCase(members.name) = ToUpperCase(mem.name)))
      {
        INSERT mem INTO members AT END;
      }
    }

    FOREVERY (RECORD smember FROM members)
    {
      o := o || GetSiteProfileMember(smember);
    }

    o := o || '  </contenttype>\n\n'
           || GetPropertyEditor(siteprofilesection
                              , screenbaseurl
                              , FALSE)
           || "\n\n"
           || GetApplies(siteprofilesection, screenbaseurl);
  }

  RETURN o;
}

STRING FUNCTION GetFileType(RECORD data, STRING baseurl)
{
  BOOLEAN addwebpage := NOT CellExists(data, "no_webpage") OR data.no_webpage = FALSE;

  STRING o;

  IF (data.isrtd)
  {
    // basically copied from /publisher/data/basesiteprofile.xml
    o := '  <filetype typedef="' || baseurl || 'filetype/' || GetSiteProfileXMLName(data.title) || '"\n'
      || '            title="' || EncodeHTML(data.title) || '"\n'
      || '            tolliumicon="tollium:files/application_x-webhare-richdocument"\n'
      || '            blobiscontent="false"\n'
      || '            needstemplate="true"\n'
      || '            needsprofile="false"\n'
      || '            ispublishable="true"\n'
      || '            requirescontent="false"\n'
      || '            isacceptableindex="true"\n'
      || '            ispublishedassubdir="true"\n'
      || '            extensions=".rtd"\n';
  }
  ELSE
  {
    o := '  <filetype typedef="' || baseurl || 'filetype/' || GetSiteProfileXMLName(data.title) || '"\n'
      || '            title="' || EncodeHTML(data.title) || '"\n'
      || '            blobiscontent="false"\n'
      || '            ispublishable="true"\n'
      || '            tolliumicon="tollium:files/text_plain"\n';

    IF (addwebpage)
    {
      o := o || '            isacceptableindex="false"\n'
             || '            ispublishedassubdir="true"\n'
             || '            needstemplate="true"\n';
    }
  }

  o := o || '            />';

  RETURN o;
}

STRING FUNCTION GetFolderType(RECORD data, STRING baseurl)
{
  RETURN '  <foldertype typedef="' || baseurl || 'foldertype/' || GetSiteProfileXMLName(data.title) || 's"\n'
      || '              title="' || EncodeHTML(data.title) || 's"\n'
      || '              tolliumicon="tollium:folders/projects"\n'
      || '              indexfile="contentlisting"\n'
      || '              protectindexfile="true"\n'
      || '              >\n'
      || '  </foldertype>\n'// we probably want to add something within
      || '\n'
      || '  <contenttype namespace="' || baseurl || 'foldertype/' || GetSiteProfileXMLName(data.title) || 's">\n'
      || '  </contenttype>';
}

STRING FUNCTION GetSiteProfileMember(RECORD data)
{
  IF (ToUpperCase(data.type) = "ARRAY")
  {
    STRING xml := '    <member name="' || GetSiteProfileXMLName(data.name) || '" type="array">\n';

    BOOLEAN hasrowtypes := CellExists(data, "settings") AND CellExists(data.settings, "types") AND Length(data.settings.types) > 0;

    IF (hasrowtypes)
      xml := xml || '      <member name="membertype" type="string" />\n';

    RECORD ARRAY childmembers := SELECT *
                                      , name := GetSiteProfileXMLName(name)
                                   FROM data.members;

    childmembers := SELECT childmembers.name
                         , type := Any(childmembers.type)
                         , comments := Any(childmembers.comments)
                      FROM childmembers
                  GROUP BY childmembers.name;

    FOREVERY (RECORD childmember FROM childmembers)
      xml := xml || '    ' || GetSiteProfileMember(childmember);

    xml := xml || "      </member>\n";

    RETURN xml;
  }

  STRING xml := '    <member name="' || GetSiteProfileXMLName(data.name) || '" type="';

  SWITCH (ToUpperCase(data.type))
  {
    CASE "STRING","TEXTAREA"
    {
      xml := xml || 'string"';
    }
    CASE "RICHDOCUMENT"
    {
      xml := xml || 'richdocument"';
    }
    CASE "FILE","IMAGE"
    {
      xml := xml || 'file"';
    }
    DEFAULT
    {
      xml := xml || data.type || '"';
    }
  }

  xml := xml || ' />';

  IF (data.comments != "")
    xml := xml || ' <!-- ' || data.comments || ' -->';

  RETURN xml || "\n";
}

PUBLIC STRING FUNCTION GetSiteProfileXMLName(STRING txt)
{
  txt := ToLowerCase(GetSafeFileName(txt));
  txt := Substitute(txt, "-", "_");
  RETURN txt;
}

STRING FUNCTION GetPropertyEditor(RECORD data, STRING baseurl, BOOLEAN isembobj)
{
  STRING xml;

  IF (NOT isembobj)
  {
    xml := xml || '  <tabsextension name="' || GetSiteProfileXMLName(data.name) || '" xmlns="http://www.webhare.net/xmlns/tollium/screens">\n'
               || '    <newtab title="' || data.title || '">\n';

    xml := xml || AddBoxesToPropertyEditor(data.members, "");
  }


  IF (NOT isembobj)
  {
    xml := xml || '    </newtab>\n'
               || '  </tabsextension>';
  }

  // add screens for array types
  RECORD ARRAY memberarraytypes := SELECT * FROM data.members WHERE type = "array";
  FOREVERY (RECORD memberarraytype FROM memberarraytypes)
  {
    IF (CellExists(memberarraytype, "settings") AND CellExists(memberarraytype.settings, "types") AND Length(memberarraytype.settings.types) >= 1)
    {
      FOREVERY (RECORD arraytype FROM memberarraytype.settings.types)
      {
        xml := xml
            || '\n\n  <screen title="' || memberarraytype.title || '"\n'
            || '              name="edit_' || GetSiteProfileXMLName(memberarraytype.name) || '_' || GetSiteProfileXMLName(arraytype.name) || '" xmlns="http://www.webhare.net/xmlns/tollium/screens" implementation="rowedit">\n'
            || '    <compositions>\n'
            || '      <record name="row" />\n'
            || '    </compositions>\n'
            || '    <body>\n';

        xml := xml || AddBoxesToPropertyEditor(arraytype.members, "row");

        xml := xml
            || '    </body>\n'
            || '    <footer>\n'
            || '      <defaultformbuttons buttons="ok cancel" />\n'
            || '    </footer>\n'
            || '  </screen>';
      }
    }
    ELSE IF (Length(memberarraytype.members) > 0)
    {
      STRING name_singular := MakeSingular(GetSiteProfileXMLName(memberarraytype.name));

      xml := xml
          || '\n\n'
          || '  <screen xmlns="http://www.webhare.net/xmlns/tollium/screens"\n'
          || '          implementation="rowedit"\n'
          || '          name="edit_' || name_singular || '"\n'
          || (screendata.use_tids ? `` : `          title="Bewerk ${memberarraytype.title}"` || "\n")
          || (screendata.use_tids ? '          gid=".' || GetSiteProfileXMLName(memberarraytype.name) || '.edit_' || name_singular || '"\n': "")
          || '          minwidth="500px"\n'
          || '          >\n'
          || '    <compositions>\n'
          || '      <record name="row" />\n'
          || '    </compositions>\n'
          || '    <body>\n';

      xml := xml || AddBoxesToPropertyEditor(memberarraytype.members, "row");

      xml := xml
          || '    </body>\n'
          || '    <footer>\n'
          || '      <defaultformbuttons buttons="ok cancel" />\n'
          || '    </footer>\n'
          || '  </screen>';
    }
  }

  RETURN xml;
}

STRING FUNCTION AddBoxesToPropertyEditor(RECORD ARRAY siteprofmembers, STRING composition_name)
{
  RECORD ARRAY boxes := [[ title := defaultboxname
                         , name := ""
                         , members := DEFAULT RECORD ARRAY
                         ]
                        ];

  FOREVERY (RECORD siteprofmember FROM siteprofmembers)
  {
    IF (siteprofmember.type iN ["image","array","richdocument"]) // own box
      INSERT [ title := siteprofmember.title, name := siteprofmember.name, members := [ siteprofmember ] ] INTO boxes AT END;
    ELSE // default box
      INSERT siteprofmember INTO boxes[0].members AT END;
  }

  STRING xml;
  FOREVERY (RECORD box FROM boxes)
  {
    IF (Length(box.members) > 0)
    {
      STRING tid_title := (screendata.use_tids
                           ? (box.title = defaultboxname ? `tid="~settings"` : `tid=".${box.name}"`)
                           : `title="${box.title}"`);

      xml := xml ||  '      <heading ' || tid_title || ' />\n';

      FOREVERY (RECORD siteprofmember FROM box.members)
        xml := xml || GetPropertyEditorElement(siteprofmember, composition_name) || "\n";

      if (#box+1 < Length(boxes))
        xml := xml || '\n';
    }
  }

  RETURN xml;
}

STRING FUNCTION GetPropertyEditorElement(RECORD siteprofmember, STRING composition_name)
{
    STRING xml;

    composition_name := composition_name ?? "contentdata";

    IF (NOT CellExists(siteprofmember, "settings"))
      INSERT CELL settings := DEFAULT RECORD INTO siteprofmember;

    STRING errorlabel := screendata.use_tids
                         ? `errorlabeltid=".${siteprofmember.name}"`
                         : `errorlabel="${siteprofmember.title}"`;

    SWITCH (siteprofmember.type)
    {
      CASE "string"
      {
        STRING specialtype;
        IF (CellExists(siteprofmember, "settings") AND RecordExists(siteprofmember.settings) AND CellExists(siteprofmember.settings, "specialtype"))
          specialtype := siteprofmember.settings.specialtype;

        IF (specialtype = "color")
        {
          /*
          // FIXME: <coloredit> is pretty useless at the moment, falling back to textedit for now
          // https://gitlab.b-lex.com/webhare/webhare/issues/335
          xml := xml || '      '
              || '<coloredit composition="' || composition_name || '" '
              || 'cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '" '
              || 'title="' || siteprofmember.title || '" '
              || (siteprofmember.required ? 'required="true" ' : '')
              || '/>';
          */
          xml := xml
              || '      <line>\n'
              || '        <textedit composition="' || composition_name || '" '
              || 'cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '" '
              || (siteprofmember.title != "" ? 'title="' || siteprofmember.title || '" ' : '')
              || (siteprofmember.required ? 'required="true" ' : '')
              || 'width="10x" '
              || '/>\n'
              || '        <text title="" value="(#cc00ff)" />\n'
              || '      </line>';
        }
        ELSE
        {
          xml := xml || '      '
              || '<textedit composition="' || composition_name || '" '
              || 'cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '" '
              || (siteprofmember.title != "" ? 'title="' || siteprofmember.title  || '" ' : '');

          IF (specialtype = "integer")
            xml := xml || 'valuetype="integer" ';

          xml := xml
              || (siteprofmember.required ? 'required="true" ' : '')
              || 'width="1pr" '
              || '/>';
        }
      }
      CASE "textarea"
      {
        xml := xml || '      '
            || '<textarea composition="' || composition_name || '" '
            || 'cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '" '
            || (siteprofmember.title != "" ? 'title="' || siteprofmember.title || '" ' : "")
            || 'width="1pr" '
            || 'height="3x" '
            || (siteprofmember.required ? 'required="true" ' : '')
            || '/>';
      }
      CASE "richdocument"
      {
          xml := xml
              || '      <richdocument composition="' || composition_name || '"\n'
              || '                    cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '"\n'
              || '                    height="1pr"\n'
              || '                    ' || errorlabel || '\n'
              || '                    rtdtype="' || (CellExists(screendata, "add_custom_rtdtype") AND screendata.add_custom_rtdtype ? `${screenbaseurl}rtd/${screendata.name}` : "your-rtd-type-url") || '"\n'
              || (siteprofmember.required ? '                    required="true"\n' : '')
              || '                    />';
      }
      CASE "array"
      {
        BOOLEAN hasrowtypes := CellExists(siteprofmember, "settings") AND CellExists(siteprofmember.settings, "types") AND Length(siteprofmember.settings.types) > 0;

        xml := xml
            || '      <arrayedit composition="' || composition_name || '"\n'
            || '                 cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '"\n'
            || (hasrowtypes ? '                 rowtypename="membertype"\n' : '')
            //|| '                 rowaddscreen=".add' || GetSiteProfileXMLName(siteprofmember.name) || '"\n'
            || (NOT hasrowtypes ? '                 roweditscreen=".edit_' || MakeSingular(GetSiteProfileXMLName(siteprofmember.name)) || '"\n' : '')
            || '                 orderable="true"\n'
            || '                 height="1pr"\n'
            || '                 minheight="200px"\n'
            || '                 ' || errorlabel || '\n'
            || (siteprofmember.required ? `                 minrows="1"\n` : "")
            || '                 >\n';

        IF (hasrowtypes)
        {
          xml := xml || '        <column type="text" name="membertype" title="Type" />\n\n';

          FOREVERY (RECORD membertype FROM siteprofmember.settings.types)
          {
              xml := xml || '      '
                  || '<rowdatatype type="' || GetSiteProfileXMLName(membertype.name) || '" '
                  || 'screen=".edit_' || GetSiteProfileXMLName(siteprofmember.name) || '_' || GetSiteProfileXMLName(membertype.name) || '" '
                  || 'title="' || membertype.title || '" '
                  || '/>\n';
          }
        }
        ELSE
        {
          FOREVERY (RECORD arraymember FROM siteprofmember.members)
          {
            SWITCH (ToUpperCase(arraymember.type))
            {
              CASE "TEXTEDIT","STRING"
              {
                xml := xml || '        <column type="text" '
                           || 'name="' || GetSiteProfileXMLName(arraymember.name) || '" '
                           || (arraymember.title != "" ? 'title="' || arraymember.title || '" ' : '')
                           || '/>\n';
              }
            }
          }
        }

        xml := xml || '      </arrayedit>';
      }
      CASE "image"
      {
        xml := xml
            || '      <panel layout="left">\n'
            || '        <imgedit composition="' || composition_name || '"\n'
            || '                 cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '"\n'
            //|| '                 title="' || (siteprofmember.title != "" ? siteprofmember.title : '') || '"\n'
            || '                 width="300px"\n'
            || '                 height="300px"\n'
            || '                 allowedactions="all refpoint"\n'
            || '                 title=""\n'
            || '                 errorlabel="' || siteprofmember.title || '"\n'
            || '                 publisher="false"\n'
            || (siteprofmember.required ? '                 required="true"\n' : '')
            || '                 />\n'
            || '      </panel>';
      }
      CASE "whfsref"
      {
        xml := xml
            || '      <p:browseforobject composition="' || composition_name || '"\n'
            || '                         cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '"\n'
            || (siteprofmember.title != "" ? '                         title="' || siteprofmember.title || '"\n' : "")
            || '                         width="1pr"\n'
            || '                         acceptfolders="false"\n'
            || '                         acceptfiles="true"\n'
            || '                         fullpath="/"\n'
            || (siteprofmember.required ? '                         required="true"\n' : '')
            || '                         />';
      }
      CASE "boolean"
      {
        xml := xml
            || '      <line layout="left">\n'
            || '        <checkbox composition="' || composition_name || '" '
            || 'cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '" '
            || 'title="" '
            || 'label="' || siteprofmember.title || '" '
            || '/>\n'
            || '      </line>';
      }
      CASE "datetime"
      {
        xml := xml || '      '
            || '<datetime composition="' || composition_name || '" '
            || 'cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '" '
            || (siteprofmember.title != "" ? 'title="' || siteprofmember.title || '" ' : "");

        STRING dttype := "date";
        IF (CellExists(siteprofmember.settings, "datetype"))
        {
          IF (siteprofmember.settings.datetype = "datetime")
            dttype := "datetime";
          ELSE IF (siteprofmember.settings.datetype = "time")
            dttype := "time";
        }

        xml := xml || 'type="' || dttype || '" ';

        IF (dttype IN ["datetime","time"])
        {
          // check for non-default precision setting (default = "minutes")
          IF (CellExists(siteprofmember.settings, "precision"))
            xml := xml || 'precision="' || siteprofmember.settings.precision || '" ';
        }

        // check for non-default storeutc setting (default = FALSE)
        IF (CellExists(siteprofmember.settings, "storeutc") AND siteprofmember.settings.storeutc = TRUE)
          xml := xml || 'storeutc="true" ';

        xml := xml
            || (siteprofmember.required ? 'required="true" ' : '')
            || '/>';
      }
      CASE "intextlink"
      {
        xml := xml || '      '
            || '<p:intextlink composition="' || composition_name || '" '
            || 'cellname="' || GetSiteProfileXMLName(siteprofmember.name) || '" '
            || (siteprofmember.title != "" ? 'title="' || siteprofmember.title || '" ' : "")
            || (siteprofmember.required ? 'required="true" ' : '')
            || 'width="1pr" '
            || '/>';
      }
      DEFAULT
      {
        IF (ToUpperCase(siteprofmember.type) NOT IN ["FIXME","TABLE","CATEGORIES","TAGS","POSITIONSELECT","AMFORMS_FORM"])
          abort(siteprofmember.type); // remove this when certain we support all fields
        xml := xml || '      <!-- ' || siteprofmember.comments || ' -->';
      }
    }

    RETURN xml;
}

STRING FUNCTION GetApplies(RECORD siteprofilesection, STRING baseurl)
{
  IF (CellExists(siteprofilesection, "screen_only") AND siteprofilesection.screen_only = TRUE)
    RETURN "";

  STRING xml;

  BOOLEAN filetype_is_rtd := CellExists(siteprofilesection, "filetype_is_rtd") AND siteprofilesection.filetype_is_rtd = TRUE;

  STRING xmlns_file := baseurl || 'filetype/' || GetSiteProfileXMLName(siteprofilesection.name);
  //IF (filetype_is_rtd)
    //xmlns_file := "http://www.webhare.net/xmlns/publisher/richdocumentfile";

  FOREVERY (RECORD foldertype FROM siteprofilesection.foldertypes)
  {
    STRING xmlns_folder := baseurl || 'foldertype/' || GetSiteProfileXMLName(foldertype.title) || 's';

    xml := xml
        || '  <apply>\n'
        || '    <to type="all" parenttype="' || xmlns_folder || '" />\n'
        || '    <denyfiletype typedef="*" />\n'
        || '    <denyfoldertype typedef="*" />\n'
        || '    <allowfiletype typedef="' || xmlns_file || '" />\n'
        || '    <bodyrenderer library="' || siteprofilesection.lib || '.whlib" objectname="' || GetSiteProfileXMLName(siteprofilesection.name) || 'spage" />\n'
        || '  </apply>\n\n'

        || '  <apply>\n'
        || '    <to type="file" filetype="' || xmlns_file || '" />\n';

    IF (filetype_is_rtd)
      xml := xml || '    <setobjecteditor name="publisher:editdocument" />\n';
    ELSE
      xml := xml || '    <setobjecteditor name="publisher:propertydialog" />\n';

    xml := xml
        || '    <extendproperties extension=".' || GetSiteProfileXMLName(siteprofilesection.name) || '" contenttype="' || baseurl || 'filetype/' || GetSiteProfileXMLName(siteprofilesection.name) || '" />\n'
        || '  </apply>\n\n';
  }

  RETURN xml;
}

PUBLIC STRING FUNCTION GenerateSiteProfileLibrary(STRING baseurl, RECORD siteprofilesection)
{
  screendata := siteprofilesection;

  STRING title_plural := siteprofilesection.title || "s";
  STRING name := GetSiteProfileXMLName(siteprofilesection.title);
  STRING name_plural := name || "s";
  STRING memberfields_detokenized := Detokenize((SELECT AS STRING ARRAY '"' || members.name || '"' FROM siteprofilesection.members), ", ");

  BOOLEAN filetype_is_rtd := CellExists(siteprofilesection, "filetype_is_rtd") AND siteprofilesection.filetype_is_rtd = TRUE;
  BOOLEAN index_is_rtd := CellExists(siteprofilesection, "index_is_rtd") AND siteprofilesection.index_is_rtd = TRUE;

  filetype_is_rtd := FALSE;

  STRING ARRAY loadlibs;

  INSERT 'wh::witty.whlib' INTO loadlibs AT END;

  loadlibs := loadlibs CONCAT [ 'mod::publisher/lib/webdesign.whlib'
                              , 'mod::system/lib/database.whlib'
                              , 'mod::system/lib/whfs.whlib'
                              ];

  IF (Right(baseurl, 1) != "/")
    baseurl := baseurl || "/";

  STRING o := 'PUBLIC OBJECTTYPE ' || title_plural || 'Page EXTEND StaticPageBase\n'
           || '<\n'
           || '  OBJECT webdesign;\n'
           || '  RECORD wittydata;\n'
           || '  BOOLEAN isindex;\n'
           || '\n'
           || '  UPDATE PUBLIC MACRO PrepareForRendering(OBJECT webdesign)\n'
           || '  {\n'
           || '    this->webdesign := webdesign;\n'
           || '\n'
           || '    this->isindex := webdesign->targetfolder->indexdoc = webdesign->targetobject->id;\n'
           || '\n'
           || '    IF (this->isindex)\n'
           || '      this->PrepareForIndex();\n'
           || '    ELSE\n'
           || '      this->PrepareForDetails();\n'
           || '  }\n'
           || '\n'
           || '  MACRO PrepareForIndex()\n'
           || '  {\n'
           || '    OBJECT ' || name || 'type := OpenWHFSType("' || baseurl || 'filetype/' || name || '");\n';

  IF (filetype_is_rtd)
    o := o || '    OBJECT rtdtype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");\n';

  o := o || '\n'
         || '    // collect ' || name_plural || '\n'
         || '    RECORD ARRAY ' || name_plural || ' := \n'
         || '         SELECT id\n'
         || '              , title\n'
         || '              , link := indexurl\n';

  IF (filetype_is_rtd)
    o := o || '              , intro := ""\n';

  o := o || '           FROM system.fs_objects\n'
         || '          WHERE parent = this->webdesign->targetfolder->id\n'
         || '                AND type = ' || (filetype_is_rtd ? 'rtd' : name) || 'type->id\n'
         || '                AND id != this->webdesign->targetfolder->indexdoc\n'
         || '                AND title != ""\n'
         || '                AND indexurl != ""\n'
         || '       ORDER BY ordering, ToUpperCase(title), id;\n'
         || '\n';

  IF (Length(siteprofilesection.members) = 0)
  {
    o := o || '    ' || name_plural || ' := ' || name || 'type->EnrichWithBulkData(' || name_plural || ', [ ' || memberfields_detokenized || ' ]);\n';
  }
  ELSE
  {
    o := o || '    ' || name_plural || ' := \n'
      || '        SELECT *\n';

    FOREVERY (RECORD siteprofmember FROM siteprofilesection.members)
    {
      SWITCH (siteprofmember.type)
      {
        CASE "datetime"
        {
          INSERT 'wh::datetime.whlib' INTO loadlibs AT END;

          //FIXME: Take 'precision' and 'storeutc' into account

          o := o || AddWhiteSpace(13) || ', ' || siteprofmember.name || ' := FormatDateTime("%d-%m-%Y", ' || siteprofmember.name || ')\n';
        }
        CASE "image"
        {
          INSERT 'mod::system/lib/cache.whlib' INTO loadlibs AT END;

          o := o || AddWhiteSpace(13) || ', ' || siteprofmember.name || ' := WrapCachedImage(' || siteprofmember.name || ', [ method := "fit", setwidth := 1920, format := "image/jpeg" ])\n';
        }
      }
    }

    o := o || AddWhiteSpace(10) || 'FROM ' || name || 'type->EnrichWithBulkData(' || name_plural || ', [ ' || memberfields_detokenized || ' ]);\n';
  }

  o := o || '\n';

  IF (index_is_rtd)
    o := o || '    OBJECT indexpagecontents := this->webdesign->OpenRTDFromFile(this->webdesign->targetobject);\n\n';

  o := o || '    this->wittydata := [ ' || name_plural || ' := ' || name_plural || '\n';

  IF (index_is_rtd)
    o := o || '                       , body := PTR indexpagecontents->RenderAllObjects()\n';

  o := o || '                       ];\n'
         || '  }\n'
         || '\n'
         || '  MACRO PrepareForDetails()\n'
         || '  {\n'
         // FIXME: Support for custom cells, like dates and RTEs
         || '    RECORD ' || name || 'data := this->webdesign->targetobject->GetInstanceData("' || baseurl || 'filetype/' || name || '");\n'
         || '\n';

  IF (filetype_is_rtd)
    o := o || '    OBJECT pagecontents := this->webdesign->OpenRTDFromFile(this->webdesign->targetobject->id);\n\n';

  o := o || '    this->wittydata := [ ' || name || 'data := ' || name || 'data\n';

  IF (filetype_is_rtd)
    o := o || '                       , body := PTR pagecontents->RenderAllObjects()\n';

  o := o || '                       ];\n'
         || '  }\n'
         || '\n'
         || '  UPDATE PUBLIC MACRO RunBody(OBJECT webdesign)\n'
         || '  {\n'
         || '    OBJECT wittylib := LoadWittyLibrary(Resolve("' || siteprofilesection.lib || '.witty"), "HTML-NI");\n'
         || '    wittylib->RunComponent(this->isindex ? "' || name_plural || 'index" : "' || name || 'details", this->wittydata);\n'
         || '  }\n'
         || '>;\n';

  // add <?wh and loadlibs
  STRING startoutput := '<?wh\n';
  loadlibs :=
      SELECT AS STRING ARRAY DISTINCT lib
        FROM ToRecordArray(loadlibs, "lib")
    ORDER BY lib LIKE "wh*" = FALSE, lib;

  FOREVERY (STRING lib FROM loadlibs)
    startoutput := startoutput || 'LOADLIB "' || lib || '";\n';

  o := startoutput || "\n" || o;

  RETURN o;
}

PUBLIC STRING FUNCTION GenerateSiteProfileWitty(RECORD siteprofilesection)
{
  screendata := siteprofilesection;

  STRING title_plural := siteprofilesection.title || "s";
  STRING name := GetSiteProfileXMLName(siteprofilesection.title);
  STRING name_plural := name || "s";
  STRING memberfields_detokenized := Detokenize((SELECT AS STRING ARRAY '"' || members.name || '"' FROM siteprofilesection.members), ", ");

  BOOLEAN filetype_is_rtd := CellExists(siteprofilesection, "filetype_is_rtd") AND siteprofilesection.filetype_is_rtd = TRUE;
  BOOLEAN index_is_rtd := CellExists(siteprofilesection, "index_is_rtd") AND siteprofilesection.index_is_rtd = TRUE;

  filetype_is_rtd := FALSE;

  STRING o := '[component ' || name_plural || 'index]\n';

  IF (index_is_rtd)
    o := o || '  RTD body: [body]\n\n';

  o := o || '  [forevery ' || name_plural || ']\n';

  IF (filetype_is_rtd)
    o := o || '    Intro: [intro]<br />\n';

  // FIXME: Print stuff based on member type
  FOREVERY (RECORD siteprofmember FROM siteprofilesection.members)
  {
    o := o || '    ' || siteprofmember.title || ': [' || siteprofmember.name || ']<br />\n'
           || '    <hr />\n';
  }

  o := o || '  [/forevery]\n'
         || '[/component]\n'
         || '\n';

  o := o || '[component ' || name || 'details]\n';

  IF (filetype_is_rtd)
  {
    o := o || '  RTD body: [body]<br />\n';
  }

  FOREVERY (RECORD siteprofmember FROM siteprofilesection.members)
  {
    o := o || '  ' || siteprofmember.title || ': [' || name || 'data.' || siteprofmember.name || ']<br />\n';
  }

  o := o || '[/component]\n';

  RETURN o;
}

STRING FUNCTION AddWhiteSpace(INTEGER numspaces)
{
  STRING text;

  FOR (INTEGER i := 0; i < numspaces; i := i + 1)
    text := text || ' ';

  RETURN text;
}

STRING FUNCTION MakeSingular(STRING txt)
{
  // if txt ends with 's', removes that 's', making it singular
  IF (Right(txt,1) = "s")
    txt := Substring(txt, 0, Length(txt) - 1);

  RETURN txt;
}

STRING FUNCTION TidOrTitle(BOOLEAN use_tid, STRING tid, STRING title)
{
  IF (use_tid)
    RETURN `tid="${tid}"`;

  RETURN `title="${title}"`;
}

PUBLIC STRING FUNCTION GenerateSiteProfileEmbObj(STRING baseurl, RECORD siteprofilesection)
{
  screendata := siteprofilesection;

  IF (Right(baseurl, 1) != "/")
    baseurl := baseurl || "/";

  screenbaseurl := baseurl;

  STRING name := GetSiteProfileXMLName(siteprofilesection.name);

  STRING o :=
`<?xml version="1.0" encoding="UTF-8" ?>
<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile"
             xmlns:p="http://www.webhare.net/xmlns/publisher/components"
`;

  IF (siteprofilesection.use_tids)
    o := o || '             gid="siteprofile.embeddedobjects"\n';

  o := o ||
`             >
`;

  IF (siteprofilesection.add_custom_rtdtype)
  {
    o := o ||
`
  <rtdtype namespace="${baseurl}rtd/${name}">
    <!-- <css path="../../shared/rtd.css" /> -->
    <blockstyles defaultstyle="NORMAL">
      <textstyle tag="NORMAL" textstyles="b i a-href sup sub strike" />
    </blockstyles>
  </rtdtype>
`;
  }

  o := o ||
`
  <widgettype namespace="${baseurl}embeddedobjects/${GetSiteProfileXMLName(siteprofilesection.name)}"
              editscreen=".${name}"
              renderlibrary="${name}.whlib"
              renderobjectname="${name}"
              wittycomponent="${name}.witty:embeddedobject"
              ${TidOrTitle(screendata.use_tids, `siteprofile.embeddedobjects.type-${siteprofilesection.name}`, siteprofilesection.originaltitle)}
              >
    <members>
`;

  // add <contenttye> with <member>s and <propertyeditor>s
  RECORD ARRAY members;
  FOREVERY (RECORD mem FROM siteprofilesection.members)
  {
    IF (NOT RecordExists(SELECT FROM members WHERE ToUpperCase(members.name) = ToUpperCase(mem.name)))
    {
      INSERT mem INTO members AT END;
    }
  }

  FOREVERY (RECORD smember FROM members)
  {
    o := o || "  " || GetSiteProfileMember(smember);
  }

  o := o || '    </members>\n'
         || '  </widgettype>';

  STRING gidortitle := siteprofilesection.use_tids
                       ? `gid=".${name}"`
                       : `title="Bewerk ${siteprofilesection.title}"`;

  o := o ||
`

  <screen xmlns="http://www.webhare.net/xmlns/tollium/screens"
          implementation="p:widgetedit"
          name="${name}"
          ${gidortitle}
          minwidth="600px"
          allowresize="true"
          savestate="size"
          >
    <compositions>
      <record name="contentdata" />
    </compositions>
    <body>
`;

  IF (Length(siteprofilesection.members) = 0)
    o := o || '      <text title="" value="Dit blok heeft geen instelbare eigenschappen." />\n';
  ELSE
    o := o || AddBoxesToPropertyEditor(siteprofilesection.members, "");

  o := o ||

`    </body>
    <footer>
      <defaultformbuttons buttons="ok cancel" />
    </footer>
  </screen>`;

  o := o || GetPropertyEditor(siteprofilesection
                            , baseurl
                            , TRUE);

  RETURN o || '\n\n</siteprofile>';
}

PUBLIC STRING FUNCTION GenerateSiteProfileLibraryEmbObj(STRING baseurl, RECORD siteprofilesection)
{
  screendata := siteprofilesection;

  STRING ARRAY loadlibs := [ 'mod::publisher/lib/widgets.whlib' ];

  RECORD ARRAY extradata;
  FOREVERY (RECORD siteprofmember FROM siteprofilesection.members)
  {
    SWITCH (siteprofmember.type)
    {
      CASE "datetime"
      {
        INSERT 'wh::datetime.whlib' INTO loadlibs AT END;

        INSERT [ name := siteprofmember.name
               , value := `FormatDateTime("%d-%m-%Y", this->data.${siteprofmember.name})`
               ] INTO extradata AT END;
      }
      CASE "image"
      {
        INSERT 'mod::system/lib/cache.whlib' INTO loadlibs AT END;

        INSERT [ name := siteprofmember.name
               , value := `WrapCachedImage(this->data.${siteprofmember.name}, [ method := "fit", setwidth := 1920, format := "image/jpeg" ])`
               ] INTO extradata AT END;
      }
      CASE "array"
      {
        INSERT [ name := siteprofmember.name
               , value := `(SELECT * FROM this->data.${siteprofmember.name})`
               ] INTO extradata AT END;
      }
      CASE "richdocument"
      {
        STRING rtdtype := CellExists(screendata, "add_custom_rtdtype") AND screendata.add_custom_rtdtype
                          ? `${baseurl}rtd/${screendata.name}`
                          : "your-rtd-type-url";

        INSERT [ name := siteprofmember.name
               , value := `PTR this->context->OpenRTD(this->data.${siteprofmember.name}, "${rtdtype}")->RenderAllObjects()`
               ] INTO extradata AT END;
      }
      CASE "intextlink"
      {
        INSERT 'mod::publisher/lib/publisher.whlib' INTO loadlibs AT END;

        INSERT [ name := siteprofmember.name
               , value := `GetInTextLinkTarget(this->data.${siteprofmember.name})`
               ] INTO extradata AT END;
      }
    }
  }

  // add <?wh and loadlibs
  STRING o := '<?wh\n\n';
  loadlibs :=
      SELECT AS STRING ARRAY DISTINCT lib
        FROM ToRecordArray(loadlibs, "lib")
    ORDER BY lib LIKE "wh*" = FALSE, lib;

  FOREVERY (STRING lib FROM loadlibs)
    o := o || 'LOADLIB "' || lib || '";\n';

  o := o ||
`
PUBLIC OBJECTTYPE ${siteprofilesection.name} EXTEND WidgetBase
<
  UPDATE PUBLIC MACRO Render()
  {
`;

  IF (RecordExists(extradata))
  {
    o := o ||
`    RECORD data :=
       CELL[ ...this->data`;

    FOREVERY (RECORD extradatarec FROM extradata)
    {
      o := o || `
           , ${extradatarec.name} := ${extradatarec.value}`;
    }

    o := o ||
`
           ];

    this->EmbedComponent(data);`;
  }
  ELSE
  {
    o := o || '    this->EmbedComponent(this->data);';
  }

o := o ||
`
  }
>;`;

  RETURN o;
}

PUBLIC STRING FUNCTION GenerateSiteProfileWittyEmbObj(RECORD siteprofilesection)
{
  screendata := siteprofilesection;

  STRING title_plural := siteprofilesection.title || "s";
  STRING name := GetSiteProfileXMLName(siteprofilesection.name);
  STRING name_plural := name || "s";
  STRING memberfields_detokenized := Detokenize((SELECT AS STRING ARRAY '"' || members.name || '"' FROM siteprofilesection.members), ", ");

  BOOLEAN filetype_is_rtd := CellExists(siteprofilesection, "filetype_is_rtd") AND siteprofilesection.filetype_is_rtd = TRUE;
  BOOLEAN index_is_rtd := CellExists(siteprofilesection, "index_is_rtd") AND siteprofilesection.index_is_rtd = TRUE;

  filetype_is_rtd := FALSE;

  STRING typetitle := siteprofilesection.use_tids
                      ? `[gettid siteprofile.embeddedobjects.type-${name}]`
                      : siteprofilesection.title;

  STRING o :=
`[component embeddedobject]
  <section class="embeddedobject embeddedobject--${GetSiteProfileXMLName(siteprofilesection.name)} embeddedobject--[if isrtdpreview]preview[else]site[/if]">
    [if isrtdpreview]
      <div class="embeddedobject__type">${typetitle}</div>
    [else]
      <div class="emb-${GetSiteProfileXMLName(siteprofilesection.name)}">
        [!
`;

  FOREVERY (RECORD siteprofmember FROM siteprofilesection.members)
  {
    o := o || '        [' || siteprofmember.name || ']\n';
  }

  o := o ||
`        !]
      </div>
    [/if]
  </section>
[/component]`;

  RETURN o;
}
